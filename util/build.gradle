plugins {
    id "java"
    id "signing"
    id "maven-publish"
    id "org.springframework.boot" version "2.7.3"
    id "io.spring.dependency-management" version "1.0.13.RELEASE"
}

group "io.jmpsl"
version "1.0.0"
sourceCompatibility = "11"

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.1"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "io.jmpsl"
            artifactId = "jmpsl-util"
            version = "1.0.0"
            from components.java

            pom {
                name = "Java Multi Purpose Spring Library - Utils"
                description = "This is a part of JMPS library for utils classes."
                url = "https://github.com/Milosz08/jmpsl"
                inceptionYear = "2022"

                licenses {
                    license {
                        name = "MIT License"
                        url = "https://opensource.org/licenses/mit-license.php"
                    }
                }

                developers {
                    developer {
                        id = "milosz08"
                        name = "Milosz Gilga"
                        email = "personal@miloszgilga.pl"
                    }
                }

                scm {
                    connection = ""
                }
            }
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
